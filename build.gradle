group 'com.uhuiapp'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'application'

sourceCompatibility = 1.8
mainClassName = 'com.uhuiapp.harness.qatool'

def svnversion = new ByteArrayOutputStream()

if (version == 'unspecified'){
    version = 'SNAPSHOT'
}
println 'Will build the ' +project.name+' version: '+version

ext {
    springGroup = 'org.springframework'
    springVersion = '3.2.16.RELEASE'
    strutsGroup = 'org.apache.struts'
    strutsVersion = '2.3.24.1'
    testNGGroup = 'org.testng'
    testNGVersion = '6.13.1'
    appiumGroup = 'io.appium'
    appiumClientVersion = '5.0.4'
}

repositories {
    maven {
        url "${nexusUrl}/content/groups/public"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

dependencies {
    compile "$testNGGroup:testng:$testNGVersion"
    compile "$appiumGroup:java-client:$appiumClientVersion"
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
//    compile 'org.seleniumhq.selenium:selenium-java:2.53.0'
    compile 'commons-configuration:commons-configuration:1.10'
    compile group: 'net.sourceforge.htmlunit', name: 'htmlunit', version: '2.25'
    compile group: 'info.cukes', name: 'cucumber-java', version: '1.2.5'
    compile group: 'net.masterthought', name: 'cucumber-reporting', version: '3.4.0'
    compile group: 'org.seleniumhq.selenium', name: 'selenium-htmlunit-driver', version: '2.52.0'
    compile group: 'com.aventstack', name: 'extentreports', version: '3.0.3'

    compile group: 'redis.clients', name: 'jedis', version: '2.9.0'

/*    compile 'org.seleniumhq.selenium:selenium-java:2.53.0'
    compile 'commons-configuration:commons-configuration:1.10'
    compile "$springGroup:spring-context:$springVersion"
    compile "$springGroup:spring-jdbc:$springVersion"

    compile 'mysql:mysql-connector-java:5.1.38'
    compile 'org.mybatis:mybatis-spring:1.2.3'
    compile 'org.mybatis:mybatis:3.3.0'
    compile group: 'net.sourceforge.htmlunit', name: 'htmlunit', version: '2.25'

    compile group: 'redis.clients', name: 'jedis', version: '2.9.0'*/

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

jar {
    project.exec {
        commandLine 'git','rev-parse','HEAD'
        standardOutput = svnversion
    }
    manifest {
        attributes(
                "project": project.name,
                "Implementation-Version": version,
                "Implementation-Date": new java.util.Date().toString(),
                "SVN-Revision": svnversion.toString(),
        )
    }
}

distributions {
    main {
        contents {
            from('conf') {
                into "conf"
            }
            from('testsuites') {
                into "testsuites"
            }
            from('testdata') {
                into "testdata"
            }
            from('drivers') {
                into "bin/drivers"
            }
        }
    }
}

startScripts {
    unixStartScriptGenerator.template = resources.text.fromFile('shell/customUnixStartScript.txt')
    windowsStartScriptGenerator.template = resources.text.fromFile('shell/customWindowsStartScript.txt')
}

distZip {
    eachFile { file ->
        String path = file.relativePath
        file.setPath(path.substring(path.indexOf("/")+1,path.length()))
    }
    includeEmptyDirs false
}

artifacts {
    archives distZip
}

uploadArchives {
    repositories {
        mavenDeployer {
            setUniqueVersion(false)
            repository(url: "${nexusUrl}/content/repositories/releases") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }
            snapshotRepository(url: "${nexusUrl}/content/repositories/snapshots") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }
        }
    }
}